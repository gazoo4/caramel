apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

android {
    adbOptions {
        timeOutInMs = 30 * 1000;
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
        release {
            keyAlias property_keyAlias
            keyPassword property_keyPassword
            storePassword property_storePassword
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "sk.berops.android.fueller"
        minSdkVersion 21
        targetSdkVersion 25
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

configurations {
    compile.exclude module: 'stax'
    compile.exclude module: 'stax-api'
    compile.exclude module: 'xpp3'
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile 'com.android.support:recyclerview-v7:+'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.5'
    compile 'com.google.android.gms:play-services:10.0.1'
    compile 'com.jjoe64:graphview:3.1.4'
    compile 'com.larswerkman:lobsterpicker:1.0.1'
    compile 'org.apache.commons:commons-math3:3.5'
    compile 'org.simpleframework:simple-xml:2.7.1'
}

afterEvaluate {
    assembleDebug.dependsOn "generatePng"
}

task generatePng(type:Exec) {
    println 'Generating PNG files from SVG images'

    def srcDir = "$workingDir/src/main/assets/svg"
    def dstDir = "$workingDir/src/main/res"

    // working dir defaults to the project dir
    commandLine "bash", "./support_utils/svgconvert.sh", "$srcDir", "$dstDir"
}